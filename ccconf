#!/usr/bin/env perl
# This tool generates Makefile for C++ by default

use strict;
use List::Util qw/max/;

$0 =~ s/.*\/(.+)$/$1/;

my @sources = <*.cc *.cpp>;
if (!scalar @sources) {
	die "$0: error: No C++ source file found\n";
}

my @objects = ();
for (@sources) {
	/(.+)(?:\.cc|\.cpp)$/;
	push @objects, $1 . '.o';
}

my $program = $sources[0];
$program =~ s/(.+)(?:\.cc|\.cpp)$/$1/;
my %options = (
	TAGS => 'ctags',
);

if (not open MAKEFILE, '>', 'Makefile') {
	die "$0: error: Failed to write to Makefile: $!\n";
}

select MAKEFILE;

print "# $0 @ARGV\n\n";

my $pgm_def = 0;
while ($_ = shift) {
	if (/(.+?)\+=(.*)/) {
		$options{$1} .= ' ' . $2;
	} elsif (/(.+?)=(.*)/) {
		$options{$1} = $2;
	} elsif (/^\+(.*)/) {
		$options{CFLAGS} .= `pkg-config --cflags $1`;
		exit 1 if $?;
		chomp $options{CFLAGS};
		$options{LDFLAGS} .= `pkg-config --libs $1`;
		chomp $options{LDFLAGS};
	} elsif (/^\-(.*)/) {
		$options{LDFLAGS} .= "-l$1";
	} else {
		if (not $pgm_def) {
			$program = $_;
			$pgm_def = 1;
		} else {
			warn "$0: warning: unused argument -- $_\n";
		}
	}
}

# Generates the Makefile

my $lkey = max map { length $_ } keys %options;
while (my ($key, $value) = each %options) {
	printf "%-${lkey}s = %s\n", $key, $value;
}

print qq{
.PHONY : all clean
all : $program
clean :
	rm -f $program @objects tags

tags : *.h @sources
	\$(TAGS) *.h @sources

$program : @objects
	\$(CXX) \$(LDFLAGS) -o $program @objects
};

for (@sources) {
	print `c++ -MM $_`;
}

